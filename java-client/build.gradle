apply plugin: 'java'
  
group = 'timetracking'
project.ext.set('mainClassName', 'timetracking.Main')

repositories {
  mavenCentral()
}

dependencies {
  runtime('org.slf4j:slf4j-api:1.7.6')
  runtime('org.slf4j:slf4j-log4j12:1.7.6')

  compile("net.java.dev.jna:jna:4.1.0")
  compile("net.java.dev.jna:jna-platform:4.1.0")

  compile("org.ektorp:org.ektorp:1.3.0")

  compile("commons-cli:commons-cli:1.2")
  
  testCompile("junit:junit:4.11")
}
 
task run(type: JavaExec) {
  main = mainClassName
  classpath sourceSets.main.runtimeClasspath
}
 
task makeDirs(description:'make all dirs for project setup') << {
  def sources = [sourceSets.main, sourceSets.test]
  sources*.allSource*.srcDirs.flatten().each { File srcDir ->
    println "making $srcDir"
    srcDir.mkdirs()
  } 
}
 
task wrap(type:Wrapper, description:"create a gradlew") {
  gradleVersion = '1.11'
}

jar {
  manifest {
    attributes("Main-Class": mainClassName)
  }
}

// uberjar adds in groovy jars to make this easy: "java -jar MyApp.jar"
task uberjar(type: Jar, dependsOn:[':compileJava']) {
  from files([sourceSets.main.output.classesDir, sourceSets.main.output.resourcesDir])
  from configurations.runtime.asFileTree.files.collect { zipTree(it) }

  manifest {
    attributes 'Main-Class': mainClassName
  }
}